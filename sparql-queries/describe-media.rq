PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX ore: <http://www.openarchives.org/ore/terms/>
PREFIX dc: <http://purl.org/dc/terms/>
DESCRIBE «resource-uri» ?resource
WHERE {
	«resource-uri»
	# the visual item's type
	crm:P2_has_type | 
	# record metadata
	crm:P70i_is_documented_in |
	crm:P70i_is_documented_in/dc:issued | 
	crm:P70i_is_documented_in/dc:modified |
	crm:P70i_is_documented_in/crm:P104_is_subject_to |
	crm:P70i_is_documented_in/crm:P104_is_subject_to/crm:P148_has_component |
	# the object(s) which the visual item depicts, and its type, descriptions, etc (but excluding visual items which depict it
	# the object's type
	crm:P138_represents| 
	crm:P138_represents/crm:P2_has_type | 
	# the object's identifiers, and their types
	crm:P138_represents/crm:P1_is_identified_by | 
	crm:P138_represents/crm:P1_is_identified_by/crm:P2_has_type | 
	# the object's materials, and their types
#	crm:P138_represents/crm:P45_consists_of | 
#	crm:P138_represents/crm:P45_consists_of/crm:P2_has_type |
	# the object's container, and its type
#	crm:P138_represents/crm:P106i_forms_part_of | 
#	crm:P138_represents/crm:P106i_forms_part_of/crm:P2_has_type | 
	# textual descriptions of the object, and their types
	crm:P138_represents/crm:P129i_is_subject_of |
	crm:P138_represents/crm:P129i_is_subject_of/crm:P2_has_type |
	# the production of the object, the sub-activities that made up that production, 
	# the parties carrying out those activities, and the role they played,
	# and the locations and dates of those activities
#	crm:P138_represents/crm:P108i_was_produced_by |
#	crm:P138_represents/crm:P108i_was_produced_by/crm:P9_consists_of |
#	crm:P138_represents/crm:P108i_was_produced_by/crm:P9_consists_of/crm:PC14_carried_out_by |
#	crm:P138_represents/crm:P108i_was_produced_by/crm:P9_consists_of/crm:PC14_carried_out_by/crm:P14.1_in_the_role_of |
#	crm:P138_represents/crm:P108i_was_produced_by/crm:P9_consists_of/crm:P7_took_place_at |
#	crm:P138_represents/crm:P108i_was_produced_by/crm:P9_consists_of/crm:P4_has_time-span | 
	# the dimensions of the object, the type of the dimensions, and their measurement unit
#	crm:P138_represents/crm:P43_has_dimension |
#	crm:P138_represents/crm:P43_has_dimension/crm:P2_has_type |
#	crm:P138_represents/crm:P43_has_dimension/crm:P2_has_type/crm:P91_has_unit |
	
	# the bundle of object images coverered by the same re-use rights which this image belongs to,
	# and the rights themselves
	ore:isAggregatedBy |
	ore:isAggregatedBy/crm:P104_is_subject_to |
	ore:isAggregatedBy/crm:P104_is_subject_to/crm:P148_has_component |
	ore:isAggregatedBy/crm:P104_is_subject_to/crm:P129i_is_subject_of |
	ore:isAggregatedBy/crm:P104_is_subject_to/crm:P129i_is_subject_of/crm:P2_has_type |	

	# the creation of the images, and the photographers
	# and the various derivative representations of that item, and their types (Piction images are typed)
	# along with their dimensions, and the types and measurement units of the dimensions
	crm:P94i_was_created_by |
	crm:P94i_was_created_by/crm:P14_carried_out_by |
	crm:P138i_has_representation |
	crm:P138i_has_representation/crm:P2_has_type |
	crm:P138i_has_representation/crm:P43_has_dimension |
	crm:P138i_has_representation/crm:P43_has_dimension/crm:P2_has_type |
	crm:P138i_has_representation/crm:P43_has_dimension/crm:P2_has_type/crm:P91_has_unit |					

	# the visual item's identifiers/names and their types
	crm:P1_is_identified_by | 
	crm:P1_is_identified_by/crm:P2_has_type
	?resource
}