PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX ore: <http://www.openarchives.org/ore/terms/>
DESCRIBE «resource-uri» ?resource
WHERE {
	«resource-uri»
	# the narrative's types, and their types
	crm:P2_has_type | 
	crm:P2_has_type/crm:P2_has_type | 
	# the narrative's identifiers/names and their types
	crm:P1_is_identified_by | 
	crm:P1_is_identified_by/crm:P2_has_type |
	# super-narratives (i.e. narratives that include this one)
	ore:isAggregatedBy |
	# sub-narratives (i.e. narratives included in this one), aggregated images, and texts
	ore:aggregates |
	ore:aggregates/crm:P2_has_type |
	ore:aggregates/crm:P2_has_type/rdf:value |
	ore:aggregates/crm:P2_has_type/dc:format |
	ore:aggregates/crm:P138i_has_representation |
	ore:aggregates/crm:P138i_has_representation/crm:P2_has_type |
  	# documents which describe the aggregated objects
  	ore:aggregates/crm:P129i_is_subject_of |
	ore:aggregates/crm:P129i_is_subject_of/crm:P2_has_type |
	# metadata about the source record
	crm:P70i_is_documented_in |
	crm:P70i_is_documented_in/crm:P104_is_subject_to |
	crm:P70i_is_documented_in/crm:P104_is_subject_to/crm:P148_has_component 
	?resource
}
